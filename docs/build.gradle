plugins {
    id "org.asciidoctor.convert" version "1.5.8" // https://github.com/asciidoctor/asciidoctor-gradle-plugin && http://asciidoctor.org/
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "org.ajoberstar.git-publish" version "1.0.1"
}

repositories {
    mavenCentral()
}

/**
 * Run ./gradlew docs:asciidoctor
 * Run with -t for continuous build (watch and build) => but ruby plugin crashed this way :/
 **/
asciidoctor {
    dependsOn jrubyPrepare

    requires = ['asciidoctor-diagram']

    gemPath = jrubyPrepare.outputDir

    resources {
        from('src/docs/asciidoc') {
            include '**/*.png', '**/*.jpg', '**/*.css', '**/*.otf', '**/*.woff', '**/*.ttf', '**/*.js', '**/*.html'
        }
    }

    attributes 'source-highlighter': 'coderay', 'linkcss': '', 'last-update-label': '', 'nofooter': ''
}

/**
 * Run ./gradlew docs:gitPublishPush to publish to gh-pages
 */
gitPublish {
    repoUri = 'git@github.com:dbourdette/cours-ecm-2018.git'
    branch = 'gh-pages'

    contents {
        from(file('build/asciidoc/html5')) {
            into '.'
        }
    }
}

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.2.1'
}
